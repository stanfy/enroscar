buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'nexus'

version = '1.0-SNAPSHOT'
group = 'com.stanfy.enroscar'

archivesBaseName = "enroscar-${project.name}"

repositories {
  mavenCentral()
  maven {
    url "file://${System.env['ANDROID_HOME']}/extras/android/m2repository/"
  }
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

dependencies {
  compile project(path: ':async', configuration: 'plain')
  compile 'com.squareup:javawriter:2.5.0'

  testCompile 'junit:junit:4.11'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile 'com.google.testing.compile:compile-testing:0.5'
  testCompile 'com.google.guava:guava:17.0'
  testCompile 'org.truth0:truth:0.20'

  testCompile group: 'org.robolectric', name: 'robolectric', version: '2.3'
  testRuntime files("${System.properties['java.home']}/../lib/tools.jar")
  testRuntime files("${System.env['ANDROID_HOME']}/platforms/android-19/android.jar")
  testRuntime 'com.netflix.rxjava:rxjava-core:0.19.1'
  testRuntime project(path: ':async-rx', configuration: 'plain')
}

tasks.withType(Test) {
  scanForTestClasses = false
  include "**/*Test.class"
}

def cleanupPom = { pom ->
  pom.dependencies.removeAll { it.groupId == project.group }
}

install.repositories.mavenInstaller.pom.whenConfigured(cleanupPom)
afterEvaluate {
  uploadArchives.repositories.mavenDeployer.pom.whenConfigured(cleanupPom)
}

modifyPom {
  project {
    name 'Enroscar Async Compiler'
    description 'Processor for @Load/@Send annotations'
    url 'https://github.com/stanfy/enroscar'
    inceptionYear '2011'

    scm {
      url 'https://github.com/stanfy/enroscar'
      connection 'scm:git:git@github.com:stanfy/enroscar.git'
      developerConnection 'scm:git:git@github.com:stanfy/enroscar.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'stanfy'
        name 'Stanfy Corp.'
      }
    }
  }
}
