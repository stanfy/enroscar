apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "enroscar-${project.name}"

boolean snapshot = "$project.version".contains("SNAPSHOT")
boolean hasCredentials = hasProperty('nexusUsername') && hasProperty('nexusPassword')

def RELEASES_REPO_URL = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
def SNAPSHOTS_REPO_URL = "https://oss.sonatype.org/content/repositories/snapshots/"

signing {
  required { !snapshot && gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {

      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: RELEASES_REPO_URL) {
        if (hasCredentials) {
          authentication(userName: nexusUsername, password: nexusPassword)
        }
      }
      snapshotRepository(url: SNAPSHOTS_REPO_URL) {
        if (hasCredentials) {
          authentication(userName: nexusUsername, password: nexusPassword)
        }
      }

      pom.project {
        name 'Enroscar'
        description 'Android Library'
        url 'https://github.com/stanfy/enroscar'
        inceptionYear '2011'

        scm {
          url 'https://github.com/stanfy/enroscar'
          connection 'scm:git:git@github.com:stanfy/enroscar.git'
          developerConnection 'scm:git:git@github.com:stanfy/enroscar.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'stanfy'
            name 'Stanfy Corp.'
          }
        }
      }

      pom.whenConfigured { def pom ->
        pom.dependencies.each {
          if (it.groupId == project.group) {
            def depProject = project(":${it.artifactId - 'enroscar-'}")
            if (depProject.hasProperty('latestStableVersion')) {
              it.version = depProject.latestStableVersion as String
            }
          }
        }
      }

    }
  }
}

task installArchives(type: Upload) {
  description "Installs the artifacts to the local Maven repository."
  configuration = configurations['archives']
  repositories {
    mavenDeployer {
      repository url: "file://${System.properties['user.home']}/.m2/repository"
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.sourceFiles
}

task testsJar(type : Jar) {
  from 'src/test/java'
  classifier = 'tests'
}

task classesJar(type : Copy) {
  into "build/libs"
  rename { "enroscar-${project.name}-${project.version}.jar" }
}
classesJar.outputs.upToDateWhen { false }

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
  testCompile testsJar
}

afterEvaluate {
  def defaultVariant = android.libraryVariants.find({ it.name.equals(android.defaultPublishConfig) })

  androidJavadocs.classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  androidJavadocs.classpath += fileTree(dir: "$project.buildDir/intermediates/exploded-aar/", include: "**/classes.jar")
  androidJavadocs.classpath += defaultVariant.javaCompile.classpath
  androidJavadocs.source += files("$project.buildDir/generated/source/r/${defaultVariant.dirName}/")
  androidJavadocs.dependsOn defaultVariant.javaCompile

  classesJar.from "build/intermediates/bundles/${defaultVariant.dirName}/classes.jar"
  classesJar.dependsOn defaultVariant.assemble
}
