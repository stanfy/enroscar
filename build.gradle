buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.1'
    classpath 'org.robolectric:robolectric-gradle-plugin:0.11.0'
  }
}

def getApplicationId = { "com.stanfy.enroscar.${it.name.replaceAll(/\W/, '').replaceAll(/enroscar/, '')}" }
def projectVersion = "2.0.alpha4-SNAPSHOT"

task wrapper(type: Wrapper) {
  gradleVersion = '1.9'
}


def checkstyleDir = new File(rootDir, 'config/checkstyle')
def checkstyleConfig = new File(checkstyleDir, 'checkstyle.xml')

def libraryModules = [
    'beans', 'content', 'assist', 'goro', 'images', 'io', 'net', 'stats', 'ui', 'async-rx', 'async'
    //'sherlock-integration'
].collect { project(":$it") }

configure(libraryModules) {

  version = projectVersion
  group = 'com.stanfy.enroscar'

  apply plugin: 'android-library'
  apply from: file("$rootDir/gradle/tests.gradle")
  apply from: file("$rootDir/gradle/publish.gradle")
  //apply plugin: 'checkstyle'

//  checkstyle {
//    configFile = checkstyleConfig
//    configProperties['basedir'] = project.projectDir
//  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }

  android {
    compileSdkVersion 19

    buildToolsVersion '19.1.0'

    defaultConfig {
      versionCode 10
      versionName projectVersion
      minSdkVersion 8
      applicationId getApplicationId(project)

      testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
      testApplicationId "${getApplicationId(project)}.test"
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }

  }

  def testDeps = [
      'com.squareup:fest-android:1.0.+',
      'com.google.mockwebserver:mockwebserver:20130706',
      'org.easytesting:fest-reflect:1.4.1',
      'org.mockito:mockito-all:1.9.5'
  ]

  dependencies {
    testDeps.each {
      testCompile it
      androidTestCompile it
    }

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
      exclude group: 'org.hamcrest'
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.1') {
      exclude group: 'org.hamcrest'
      exclude group: 'org.mockito'
    }

    testCompile group: 'org.robolectric', name: 'robolectric', version: '2.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.easytesting:fest-util:1.2.5'
  }

  project.ext.dependOnProjects = { list ->
    project.dependencies {
      list.each {
        compile project(it)
        testCompile project(path : it, configuration : 'testCompile')
      }
    }
  }

  android {
    if (project.hasProperty('preDex')) {
      dexOptions {
        preDexLibraries = Boolean.valueOf("$project.preDex")
      }
    }
  }

}
